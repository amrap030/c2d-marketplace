import "hashes/sha256/1024bitPadded.code" as sha256_1024;
import "hashes/sha256/512bitPadded.code" as sha256_512;
import "hashes/sha256/256bitPadded.code" as sha256_256;
import "signatures/verifyEddsa.code" as verifyEddsa;
import "ecc/babyjubjubParams.code" as context;
import "utils/pack/u32/nonStrictUnpack256.code" as unpack;
import "utils/pack/u32/pack256.code" as pack256;
import "utils/casts/u32_to_field.code" as u32_to_field;
from "ecc/babyjubjubParams" import BabyJubJubParams;

def main(private u32[32] mut values, private field nonce, private field[2] R, private field S, field[2] A, u32[8] M) -> field {
    // verify signature
    BabyJubJubParams context = context();
    bool isVerified = verifyEddsa(R, S, A, M, M, context);
    assert(isVerified);

    // calculate hash of input values from dataset
    u32[8] hash = sha256_1024(values[0..8],values[8..16],values[16..24],values[24..32]);
    
    // check integrity of input values
    bool isHashMatching = hash == M;
    assert(isHashMatching);

    // calculate the mean
    u32 mut sum = 0;
    for u32 i in 0..32 {
        sum = sum + values[i];
    }
    u32 mean = sum / 32;
    
    // calculate the variance
    for u32 i in 0..32 {
        values[i] = (values[i] - mean) * (values[i] - mean);
    }

    sum = 0;
    for u32 i in 0..32 {
        sum = sum + values[i];
    }

    u32 variance = sum / 32;

    // calculare standard deviation
    u32 mut lo = 0;
    u32 mut hi = variance;

    for u32 i in 0..32 {
        u32 mid = (lo + hi) / 2;
        hi = mid * mid > variance ? mid - 1 : hi;
        lo = mid * mid > variance ? lo : mid + 1;
    }

    field std = u32_to_field(hi);
    log("[Witness] result={}", std);

    // create merkle tree
    u32[8] h_computation = sha256_256(unpack(std));
    u32[8] h_nonce = sha256_256(unpack(nonce));

    field result = pack256(sha256_512(h_computation, h_nonce));

    return result;
}